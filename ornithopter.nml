grf {
    grfid: "KK\11\01";
    name: string(STR_GRF_NAME);
    desc: string(STR_GRF_DESC);
    version: 0;
    min_compatible_version: 0;
}


/* 48x16x48 */
template tmpl_ac_sm(x,y) {
    //[left_x,      upper_y,    width,     height,     offset_x,     offset_y]
	[ x+121,	0+y,	21, 32,	-11,	-16]
	[ x+143,	0+y,	32, 32,	-16,	-16]
	[ x+176,	0+y,	32, 24, -16,    -12]
	[ x+209,	0+y,	32, 32,	-16,	-16]
	[ x+0,	    0+y,	21, 32,	-11,	-16]
	[ x+22,	    0+y,	32, 32,	-16,	-16]
	[ x+55,	    0+y,	32, 24, -16,    -12]
	[ x+88,	    0+y,	32, 32,	-16,	-16]
}



template tmpl_ac_sm_purchase(x,y) {
	[ x+55,	    0+y,	32, 24, -16,    -12]
}

spriteset(ss_ornithopter1, "gfx/ornithopter1.png") {
    tmpl_ac_sm(0,0)
}
spriteset(ss_ornithopter2, "gfx/ornithopter2.png") {
    tmpl_ac_sm(0,0)
}
spriteset(ss_ornithopter3, "gfx/ornithopter3.png") {
    tmpl_ac_sm(0,0)
}

spriteset(ss_ac_purchase, "gfx/ornithopter3.png") {
    tmpl_ac_sm_purchase(0,0)
}

spriteset(no_rotor, "gfx/empty.png") {
   [0, 0]
}

switch (FEAT_AIRCRAFT, SELF, sw_ornithopter_anim, (motion_counter % 16)) {
    0..1:  ss_ornithopter1;
    2..3:  ss_ornithopter2;
    4..5:  ss_ornithopter1;
    6..7:  ss_ornithopter3;
    8..9:  ss_ornithopter1;
    10..11:  ss_ornithopter2;
    12..13:  ss_ornithopter1;
    14..15:  ss_ornithopter3;
}


// change the color depending on the cargo type
switch (FEAT_AIRCRAFT, SELF, sw_ac_colour, cargo_classes) {
    bitmask(CC_PASSENGERS): palette_2cc(company_colour1, company_colour2);
    palette_2cc(company_colour2, company_colour1);
}

/* Define the vehicle */
item(FEAT_AIRCRAFT, air_c142) {
    property {
        /* Properties common to all vehicle types */
        name: string(STR_ORNITHOPTER_NAME);
        climates_available: ALL_CLIMATES;
        introduction_date: date(2000,01,01);
        model_life: 255;
        vehicle_life: 30;
        reliability_decay: 10;
        refittable_cargo_classes: ALL_NORMAL_CARGO_CLASSES;
        loading_speed: 20;
        cost_factor: 10;
        running_cost_factor: 100;
        cargo_age_period: 185; //74=1 day

        /* Aircraft-specific properties */
        sprite_id: SPRITE_ID_NEW_AIRCRAFT;
        speed: 500 km/h;
        range: 512; // 0 for unlimited, 0..2894
        misc_flags: bitmask(AIRCRAFT_FLAG_2CC, AIRCRAFT_FLAG_AUTOREFIT);
        refit_cost: 0; // Refitting is free
        aircraft_type: AIRCRAFT_TYPE_HELICOPTER;
        acceleration: 10;
        passenger_capacity: 32;
        mail_capacity: 8;
        sound_effect: SOUND_HELICOPTER;
    }

    /* Define graphics for various cargo types, as well as the capacity callback */
    graphics {
        purchase: ss_ac_purchase;
        default: sw_ornithopter_anim;
        rotor: no_rotor;
        colour_mapping: sw_ac_colour;
    }
}
